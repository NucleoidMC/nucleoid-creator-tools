plugins {
  id 'fabric-loom' version '1.8.+'
  id 'maven-publish'
  id "com.modrinth.minotaur" version "2.+"

}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
  accessWidenerPath = file("src/main/resources/creator_tools.accesswidener")
}

repositories {
  maven { url = "https://maven.nucleoid.xyz/" }
  maven { url = "https://maven.gegy.dev/" }
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

}

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

  modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

  modImplementation include('eu.pb4:polymer-core:0.11.0+1.21.4-rc1')
  modImplementation include('eu.pb4:polymer-virtual-entity:0.11.0+1.21.4-rc1')
  modImplementation include('xyz.nucleoid:server-translations-api:2.4.0+1.21.2-rc1')
  modImplementation include('xyz.nucleoid:fantasy:0.6.5+1.21.2')
  modImplementation(include('me.lucko:fabric-permissions-api:0.3.3'))

  modImplementation include('xyz.nucleoid:map-templates:0.2.1+1.21.3')
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  it.options.release = 21
}

java {
  withSourcesJar()
}

jar {
  from("LICENSE") {
    rename { "${it}_${project.archivesBaseName}"}
  }
}

if (System.getenv("MODRINTH")) {
  modrinth {
    token = System.getenv("MODRINTH")
    projectId = 'HDocER98'// The ID of your modrinth project, slugs will not work.
    versionNumber = "" + version // The version of the mod to upload.
    versionType = "release"
    uploadFile = remapJar // This links to a task that builds your mod jar and sets "uploadFile" to the mod jar.
    gameVersions = [((String) project.minecraft_version)]
    loaders = ["fabric", "quilt"]
  }

  remapJar {
    finalizedBy project.tasks.modrinth
  }
}